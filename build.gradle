plugins {
    id "architectury-plugin" version "3.4+"
    id "dev.architectury.loom" version "1.1.+" apply false
    id "com.teamresourceful.resourcefulgradle"
}

architectury {
    minecraft = mc_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:$mc_version"
        mappings loom.officialMojangMappings()
    }

    repositories {
        maven { url "https://maven.resourcefulbees.com/repository/maven-public/" }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = archives_base_name
    version = libs.versions.mod.version.get()
    group = maven_group

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }

    publishing {
        if (project != rootProject) {
            publications {
                mavenJava(MavenPublication) {
                    artifactId = "$archives_base_name-$project.name-$mc_version"
                    from components.java

                    pom {
                        name = 'ResourcefulLib'
                        description = 'The library behind Team Resourceful mods and more.'
                        url = 'https://github.com/Team-Resourceful/ResourcefulLib'

                        scm {
                            connection = 'git:https://github.com/Team-Resourceful/ResourcefulLib.git'
                            developerConnection = 'git:https://github.com/Team-Resourceful/ResourcefulLib.git'
                            url = 'https://github.com/Team-Resourceful/ResourcefulLib'
                        }

                        licenses {
                            license {
                                name = 'MIT'
                            }
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                url = nightly.toBoolean()
                        ? "https://nexus.resourcefulbees.com/repository/maven-nightly/"
                        : "https://nexus.resourcefulbees.com/repository/maven-releases/"
                credentials {
                    def ENV = System.getenv()
                    username ENV.MAVEN_USER
                    password ENV.MAVEN_PASS
                }
            }
        }
    }
}

resourcefulGradle {
    templates {
        readme {
            source = file("templates/README.md.template")
            injectedValues = [
                    "version"            : libs.versions.mod.version.get(),
                    "mc_version"         : mc_version
            ]
        }
        discordEmbed {
            var fabricLink = System.getenv("FABRIC_RELEASE_URL")
            var forgeLink = System.getenv("FORGE_RELEASE_URL")

            injectedValues = [
                    "version"      : libs.versions.mod.version.get(),
                    "mc_version"   : mc_version,
                    "forge_version": forge_version,
                    "fabric_version": fabric_loader_version,
                    "fabric_link": fabricLink,
                    "forge_link": forgeLink
            ]

            def template = nightly.toBoolean() ? "nightly_embed.json.template" : "release_embed.json.template"
            source = file("templates/$template")
        }
    }
}
